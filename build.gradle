plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
}

group = 'org.korz.pygments'
version = '2.0.0'

repositories {
    jcenter()
}

dependencies {
    implementation "org.python:jython-standalone:2.7.1"
    implementation "org.pygments:pygments:2.2.0"

    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "com.google.guava:guava:23.3-jre"
}

test {
    testLogging {
        showStandardStreams true
        exceptionFormat 'full'
    }
}

jacoco {
    toolVersion = '0.7.9'
}

javadoc {
    options {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'http://www.jython.org/javadoc/'
        // these classes are not intended to be subclassed
        showFromPublic()
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_API_KEY
    publications = ['maven']
    pkg {
        userOrg = 'okorz001'
        repo = 'maven'
        name = rootProject.name
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
    distributionType = 'all'
}
